// Gradle settings and tasks common to all subprojects

apply plugin: 'java'

group = 'com.github.stephengold'

sourceCompatibility = '1.7'
targetCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // Java compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // Java runtime options:
        classpath sourceSets.main.runtimeClasspath
        //debug true
        //doFirst { println commandLine }
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xbatch'
        //jvmArgs '-Xms512m', '-Xmx512m'
        //jvmArgs '-XX:+PrintCompilation'
        //jvmArgs '-XX:+UseConcMarkSweepGC'
        jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext {
    // current versions of various libraries
    jcommanderVersion = '1.81'
    heartVersion = '6.4.3+for34'
    jme3utilitiesniftyVersion = '0.9.19+for34'
    jme3utilitiesuiVersion = '0.9.3+for34'
    jmonkeyengineVersion = '3.4.0-beta1'
    skycontrolVersion = '0.9.31+for34'
}

repositories {
    //mavenLocal()
    mavenCentral()
}

// custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar) {
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of source files.'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}
